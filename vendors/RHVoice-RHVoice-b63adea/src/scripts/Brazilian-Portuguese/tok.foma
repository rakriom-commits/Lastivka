# Copyright (C) 2019, 2020  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

source bp_roman.foma

define Apos %'|%’ ; 

define VowelLetter 
A|À|Á|Â|Ã | E|É|Ê | I|Í | O|Ó|Ô|Õ | U|Ú|Ü | Y | Ö | 
a|à|á|â|ã | e|é|ê | i|í | o|ó|ô|õ | u|ú|ü | y | ö ; 

define ConsonantLetter 
B|C|Ç|D|F|G|H|J|K|L|M|N|P|Q|R|S|T|V|W|X|Z|Ñ | 
b|c|ç|d|f|g|h|j|k|l|m|n|p|q|r|s|t|v|w|x|z|ñ ; 

define Letter VowelLetter | ConsonantLetter ; 

define ExceptionalWord 
[[e|E] [t|T] [c|C] %.] | 
[[w|W] [i|I] %- [f|F] [i|I]] ; 

define Word [Letter+ [Apos Letter+]*] | ExceptionalWord ; 

define LetterSequence [ConsonantLetter+]/Apos ; 

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;

define FormattedNumber NZDigit Digit^<3 [%. Digit^3]^{1,4} ; 

define BasicTokenizer 
Word @-> ... word || _ \Letter | .#. ,, 
FormattedNumber @-> ... num || _ .#. | \Digit ,, 
NZDigit Digit* @-> ... num ,, 
%0 Digit+ @-> ... dig ,, 
%0 @-> ... num ,, 
\[Letter | Digit] @-> ... sym ; 

define UntagLargeNumbers 
num -> dig || Digit^16 _ ; 

define TagLetterSequences 
word -> lseq || [.#.|word|num|dig|sym] LetterSequence _ ; 

define TagRomanNumerals 
word -> num || [.#.|sym] BPRomanNumeral _ [.#.|[? sym]] ; 

define F123Rule 
num -> dig || [.#. | sym] F lseq {123} _ ; 

define TagOrdinalNumbers 
[num:0] [%º|%ª] [sym:num] -> ; 

define Tokenizer 
BasicTokenizer .o. 
TagRomanNumerals .o. 
TagLetterSequences .o. 
UntagLargeNumbers .o. 
TagOrdinalNumbers .o. 
F123Rule ; 

regex Tokenizer ; 
